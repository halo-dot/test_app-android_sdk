// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        agp_version = '7.4.2'
    }
    ext.kotlin_version = '1.9.10'
    ext.dokka_version = '1.4.32'

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven {
            setUrl("https://jitpack.io")
        }
    }

    dependencies {
        // NOTE: Do not place your application dependencies here;
        // they belong in the individual module build.gradle files

        classpath 'com.android.tools:r8:8.1.56'  // Must be before the Gradle Plugin for Android.
        classpath "com.android.tools.build:gradle:$agp_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

ext {
    Properties properties = new Properties()
    def propertiesFile = project.rootProject.file('local.properties')
    if (propertiesFile.exists()) {
        properties.load(propertiesFile.newDataInputStream())
    }

    accessKey = properties.getProperty('aws.accessKey')
    secretKey = properties.getProperty('aws.secretKey')
    tokenKey = System.getenv('AWS_SESSION_TOKEN')
}

allprojects {
    repositories {
        def repos = ['releases', 'snapshots']
        repos.each { repo ->
            maven {
                name = repo
                url = "s3://synthesis-halo-artifacts/$repo"

                credentials(AwsCredentials) {
                    accessKey = rootProject.ext.accessKey
                    secretKey = rootProject.ext.secretKey
                    if (tokenKey != null) {
                        sessionToken = rootProject.ext.tokenKey
                    }
                }
            }
        }

        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}